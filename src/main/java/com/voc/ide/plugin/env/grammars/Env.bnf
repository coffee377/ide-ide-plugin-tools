{
  parserClass="com.voc.ide.plugin.env.parser.EnvParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Env"
  psiImplClassSuffix="Impl"
  psiPackage="com.voc.ide.plugin.env.psi"
  psiImplPackage="com.voc.ide.plugin.env.psi.impl"
  psiImplUtilClass="com.voc.ide.plugin.env.psi.impl.EnvPsiImplUtil"

  elementTypeHolderClass="com.voc.ide.plugin.env.psi.EnvTypes"
  elementTypeClass="com.voc.ide.plugin.env.psi.EnvElementType"
  tokenTypeClass="com.voc.ide.plugin.env.psi.EnvTokenType"

}

envFile ::= item_*

private item_ ::= (EXPORT? property|COMMENT|CRLF)

key ::= KEY_CHARS

value ::= VALUE_CHARS+ | QUOTE VALUE_CHARS* QUOTE?

property ::= (key SEPARATOR value? COMMENT?) | (key COMMENT?)  {
 implements="com.voc.ide.plugin.env.psi.EnvNamedElement"
 mixin="com.voc.ide.plugin.env.psi.impl.EnvNamedElementImpl"
 methods=[getKeyText getValueText getName setName getNameIdentifier getPresentation]
}
